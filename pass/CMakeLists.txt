add_library(ProteusPass SHARED ProteusPass.cpp)

target_include_directories(ProteusPass
  SYSTEM PRIVATE ${LLVM_INCLUDE_DIRS})

if(NOT LLVM_ENABLE_RTTI)
  target_compile_options(ProteusPass PRIVATE -fno-rtti)
endif()

# Allow undefined symbols in shared objects on Darwin (this is the default
# behaviour on Linux)
target_link_libraries(ProteusPass PUBLIC
  "$<$<PLATFORM_ID:Darwin>:-undefined dynamic_lookup>")

if(ENABLE_PROTORCH)
  find_package(ProTorch REQUIRED)

  target_link_libraries(ProteusPass PRIVATE ${ProTorch_LIBRARIES})
  target_include_directories(ProteusPass 
                SYSTEM PRIVATE ${ProTorch_INCLUDE_DIRS})
endif()

target_compile_options(
  ProteusPass
  INTERFACE
  -fpass-plugin=$<TARGET_FILE:ProteusPass>
)

target_link_options(
  ProteusPass
  INTERFACE
  "SHELL:$<$<LINK_LANGUAGE:HIP>:-Xoffload-linker --load-pass-plugin=$<TARGET_FILE:ProteusPass>>"
)

install(
  TARGETS ProteusPass
  EXPORT proteusTargets
  RUNTIME DESTINATION lib
  LIBRARY DESTINATION lib
  ARCHIVE DESTINATION lib)
